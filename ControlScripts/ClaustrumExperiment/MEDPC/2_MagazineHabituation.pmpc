\***************************************************
\               File Description Comments
\***************************************************

\ Magazine Habituation by SeÃ¡n Martin on 08/07/2019
    \ Brief Description: Drop a pellet every 30 seconds.
    \ Ending criteria: Ends after 15mins (Default)
    \ Locked pins: Remember to turn off the fan 

\***************************************************
\               Constant Definitions
\***************************************************
\ Experiment variable locations
    ^MaxTimeIdx = 0
    ^RateDropIdx = 1

\ Index holder locations
    ^FeederTimeIdx = 0
    ^NosepokeTimeIdx = 1

\ Counter locations
    ^FeederCountIdx = 0
    ^NosepokeCountIdx = 1

\ Inputs
    ^Nosepoke = 7

\ Outputs
    ^HouseLight = 7
    ^Feeder = 9
    ^FanBox = 16

\***************************************************
\               Variable Descriptions
\***************************************************

DIM C = 1
\ Counter descriptions
    \ C(0) = Number of Pellets dropped
    \ C(1) = Number of Nosepokes

DIM A = 1
DIM B = 1
DIM D = 50 \ Should be large enough to hold rate * exp_time
DIM E = 5000 \ Theoretically this could be anything - checks
\ Arrays
    \ A = Experiment variables
    \ B = Index holders
    \ D = Data array for Feeder drop times
    \ E = Data array for nosepoke times

\ Aliases
    \ TODO one of these aliases is temp
    VAR_ALIAS Maximum Time Minutes = A(^MaxTimeIdx) \ Default 15 minutes
    VAR_ALIAS Drop Rate Seconds = A(^RateDropIdx) \ Default 30 seconds
    VAR_ALIAS Rate Ticks = G \ Just a test!
    VAR_ALIAS Test = H \ Also just a test!

\ Z Pulses
    \ Z1 = Screen update pulse
    \ Z2 = Activate Feeder
    \ Z32 = End the session

\ Single Variables
    \ T = Elapsed time

\ TODO remove G and H
DISKVARS = A, C, D, E, T, G, H,

\***************************************************
\               Implementation
\***************************************************

S.S.1, \ Control flow

    S1, \ Establish default values and turn on fan
        0.01":
            SET A(^MaxTimeIdx) = 15;
            SET A(^RateDropIdx) = 30;
            LOCKON ^FanBox
        ---> S2

    S2, \ Wait for start signal
        #START:
            ON ^HouseLight;
            Z1
        ---> S3

    S3, \ Time the session and flag off after Max Time
        0.01":
            SET T = T + 0.01;
            IF T/60 >= A(^MaxTimeIdx) [@EndSession, @ContinueSession]
                @End: Z32 ---> S4
                @Cont: ---> SX
            ENDIF

    S4, \ Stop the session
        3":
        ---> STOPABORTFLUSH

\***************************************************
\                Main Program
\***************************************************
S.S.2, \ Experiment logic

    S1, \Wait for experiment start
        #START:
            SET G = A(^RateDropIdx) * 1";
            SET A(^RateDropIdx) = A(^RateDropIdx) * 1";
            SET H = A(^RateDropIdx)
        ---> S2
    S2, \ Drop pellets at desired rate
        A(^RateDropIdx)#T:
            Z2
        ---> SX       

S.S.5 \ Detect nosepokes and record the times

    S1, \Wait for experiment start
        #START:
        ---> S2
    S2, \ Detect nosepokes
        1 #R ^Nosepoke:
            SET E(B(^NosepokeTimeIdx)) = T;
            ADD B(^NosepokeTimeIdx);
            ADD C(^NosepokeCountIdx);
            SET E(B(^NosepokeTimeIdx)) = -987.987;
            Z1
        ---> S3
    S3, \ Time out after a poke to avoid multiple recordings
        0.1":
        ---> S2

S.S.6, \ Drop pellets

    S1, \ Drop the pellet
        #Z2:
            ON ^Feeder;
            SET D(B(^FeederTimeIdx)) = T;
            ADD B(^FeederTimeIdx);
            ADD C(^FeederCountIdx);
            SET D(B(^FeederTimeIdx)) = -987.987;
            Z1
        ---> S2

    S2, \ Turn off the feeder after dropping
        0.5": 
            OFF ^Feeder
        ---> S1


S.S.10, \ Update the display

    S1, \ Wait to read a Z pulse
        #Z1:
        ---> S2
    S2, \ Update the screen
        .01":
            SHOW 1, Number of Pellets, C(0);
            SHOW 2, Number of Nosepokes, C(1);
            SHOW 3, Last Event Time, T
        ---> S1
